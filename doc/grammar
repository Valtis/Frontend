Notes:
-e stands for epsilon
-Lowercase text are terminals, uppercase are nonterminals 
-whitespace is non-significant
-this is work in progress and is expanded as needed
-function return type is considered to be 'void' if no return type is specified


S -> FUNCTION S | e

FUNCTION -> fn PARAM_LIST OPTIONAL_RETURN_TYPE BLOCK

PARAM_LIST -> ( OPT_FUNCTION_PARAMS )

OPT_FUNCTION_PARAMS -> FUNCTION_PARAMS 
                     | e

FUNCTION_PARAMS -> FUNCTION_PARAM 
                 | FUNCTION_PARAM, FUNCTION_PARAMS 
                 
FUNCTION_PARAM -> identifier : VALUE_TYPE

OPTIONAL_RETURN_TYPE : ANY_TYPE | e

BLOCK -> { STATEMENTS }

STATEMENTS -> STATEMENT; STATEMENTS | e

STATEMENT -> VARIABLE_DECLARATION 
	     | BLOCK

VARIABLE_DECLARATION -> let identifier : VALUE_TYPE = EXPRESSION

EXPRESSION -> TERM PLUS_MINUS_EXPRESSION

PLUS_MINUS_EXPRESSION ->  + TERM PLUS_MINUS_EXPRESSION
		        | - TERM PLUS_MINUS_EXPRESSION
                        | e

TERM -> FACTOR MULT_DIV_TERM

MULT_DIV_TERM -> * FACTOR MULT_DIV_TERM
               | / FACTOR MULT_DIV_TERM
               | e

FACTOR -> ( EXPRESSION ) 
        | identifier 
        | CONSTANT_VALUE


VALUE_TYPE -> int_type
      | float_type
      | double_type
      | boolean_type
      | string_type 

ANY_TYPE -> VALUE_TYPE | void_type

CONSTANT_VALUE -> integer_token
                | float_token
                | double_token
                | boolean_token
                | string_token  

	note on CONSTANT_VALUE: for numbers, +/- are valid start tokens (eg. "+" token followed by a int/float/double would be accepted)


Error recovery

On error, parser should emit a diagnostic message and skip to next valid same level construct and continue parsing.




